Schedule:
	Every | Selective
;

Every:
	'every' n=INT unit=Unit (when=CallTime)?
;

Unit:
	'hours' | 'mins' | 'minutes'
;

Time:
	time = /([0-9]|0[0-9]|1[0-9]|2[0-3]):[0-5][0-9]/
;

From:
	'from' start=Time 'to' end=Time
;

Synchronized:
	'synchronized'
;

CallTime:
	From | Synchronized
;

Ordinal:
	'1st'|'2nd'|'3rd'|'4th'|'5th'|'6th'|'7th'|'8th'|'9th'|'10th'|'11th'|'12th'|
	'13th'|'14th'|'15th'|'16th'|'17th'|'18th'|'19th'|'20th'|'21st'|'22nd'|'23rd'|
	'24th'|'25th'|'26th'|'27th'|'28th'|'29th'|'30th'|'31st'
;

Day:
	'mon'|'tue'|'wed'|'thu'|'fri'|'sat'|'sun'|'every day'
;

TimeSpecial:
	Every | Time
;

Selective:
	ordinal=Ordinal[','] days=Day[','] (monthspec=Monthspec)? when=TimeSpecial
;

Month:
	'jan'|'feb'|'mar'|'apr'|'may'|'jun'|'jul'|'aug'|'sep'|'oct'|'nov'|'dec'
;

Monthspec:
	'of' months=Month[',']
;