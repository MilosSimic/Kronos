Schedule:
	Every | Selective
;

Every:
	'every' n=INT unit=Unit (when=CallTime)?
;

Unit:
	'hours' | 'mins' | 'minutes'
;

Time:
	time = /([0-9]|0[0-9]|1[0-9]|2[0-3]):[0-5][0-9]/
;

From:
	'from' start=Time 'to' end=Time
;

Synchronized:
	'synchronized'
;

CallTime:
	From | Synchronized
;

Ordinal:
	'every ordinal'|'1st'|'2nd'|'3rd'|'4th'|'5th'|'6th'|'first'|'second'|'third'|'fourth'|'fifth'|'sixth'
;

Day:
	'every day'|'mon'|'tue'|'wed'|'thu'|'fri'|'sat'|'sun'
;

OnTime:
	('at')? ontime=Time
;

TimeSpecial:
	Every | OnTime
;

Selective:
	ordinal=Ordinal[','] days=Day[','] 'of' months=Month[','] when=TimeSpecial
;

Month:
	'every month'|'jan'|'feb'|'mar'|'apr'|'may'|'jun'|'jul'|'aug'|'sep'|'oct'|'nov'|'dec'
;